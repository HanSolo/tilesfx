import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.3.1'
    }
}

// TilesFX main build file
plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '2.10.0'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

apply plugin: 'biz.aQute.bnd.builder'

sourceCompatibility = '11'

wrapper {
    gradleVersion = '6.4'
}

description = 'TilesFX is a JavaFX library containing tiles for dashboards'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

dependencies {
}

// Supress javadoc warnings
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

javafx {
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.web' ]
}

jar {
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date': project.buildDate,
                'Build-Time': project.buildTime,
                'Build-Revision': versioning.info.commit,
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Bundle-Name': project.name,
                'Bundle-License': 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://www.eclipse.org/legal/eplfaq.php',
                'Bundle-Description': description,
                'Bundle-SymbolicName': 'eu.hansolo.tilesfx',
                'Export-Package': 'eu.hansolo.tilesfx,eu.hansolo.tilesfx.chart,eu.hansolo.tilesfx.colors,eu.hansolo.tilesfx.events,eu.hansolo.tilesfx.fonts,eu.hansolo.tilesfx.skins,eu.hansolo.tilesfx.tools,eu.hansolo.tilesfx.weather'
        )
    }
}

repositories {
    jcenter()
    mavenCentral()
}

// start the demo from gradle
task Demo(type: JavaExec) {
    main = "eu.hansolo.tilesfx.DemoLauncher"
    classpath = sourceSets.main.runtimeClasspath
}

// Only used for internal test
task Test(type: JavaExec) {
    main = "eu.hansolo.tilesfx.TestLauncher"
    classpath = sourceSets.main.runtimeClasspath
}

// create one jar for the javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def pomConfig = {
    name 'tilesfx'
    // Description won't be used when creating the pom file automatically.
    // So I've added it manually in the pom.withXml{} section in the publishing task
    //description 'TilesFX is a library containing tiles for dashboards'
    url 'https://github.com/HanSolo/tilesfx/wiki'
    inceptionYear '2016'
    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'scm:git@github.com:HanSolo/tilesfx.git'
        connection 'scm:git@github.com:HanSolo/tilesfx.git'
        developerConnection 'scm:git@github.com:HanSolo/tilesfx.git'
    }
    developers {
        developer {
            id 'HanSolo'
            name 'Gerrit Grunwald'
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + pomConfig
                asNode().appendNode('description', description)
                asNode().dependencies.'*'.findAll() {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.remove(it.classifier)
                }
            }
        }
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''
if (!project.hasProperty('sonatypeUsername')) ext.sonatypeUsername = ''
if (!project.hasProperty('sonatypePassword')) ext.sonatypePassword = ''


bintray {
    user = project.bintrayUsername
    key  = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo                  = 'tilesfx'
        userOrg               = 'hansolo'
        name                  = project.name
        desc                  = description
        licenses              = ['Apache-2.0']
        labels                = ['javafx']
        websiteUrl            = 'https://github.com/HanSolo/tilesfx/wiki'
        issueTrackerUrl       = 'https://github.com/HanSolo/tilesfx/issues'
        vcsUrl                = 'git@github.com:HanSolo/tilesfx.git'
        publicDownloadNumbers = true
        version {
            mavenCentralSync {
                sync = true
                user = project.sonatypeUsername
                password = project.sonatypePassword
                close = '1'
            }
        }
    }
}